UnicodeLetter = /* Any Unicode codepoint in Category L */ .
Letter        = UnicodeLetter | "_" .
Digit         = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .

Identifier   = Letter { Letter | Digit } .
IntLiteral   = Digit { Digit } .
ArithmeticOP = "+" | "-" .

Program    = "begin" StatementList "end" .
Statement  = (Identifier ":=" Expression |
             "read" "(" IdentifierList ")" |
             "write" "(" ExpressionList ")") ";" .
Expression = Primary { ArithmeticOP Primary } .

StatementList  = Statement { Statement } .
IdentifierList = Identifier { "," Identifier } .
ExpressionList = Expression { "," Expression } .

Primary = "(" Expression ")" |
          Identifier |
          IntLiteral .
